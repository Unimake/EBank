





FUNCTION Main()
   LOCAL aOpcoes, nOpcao

   aOpcoes := {}

   AAdd(aOpcoes, "Autenticar API")
   AAdd(aOpcoes, "Gerar PIX")
   AAdd(aOpcoes, "Consultar PIX")
   AAdd(aOpcoes, "Registrar Boleto")
   AAdd(aOpcoes, "Consultar Boleto")


   while .T.
      Scroll() ; SetPos(0,0)

      DevPos( 1, 2 ) ; DevOut( "API Unimake for " + Version() )

      nOpcao := Achoice( 3, 2, 30, 80, aOpcoes)

      Scroll() ; SetPos(0,0)

      DO CASE
         CASE LastKey() == 27
            EXIT

         CASE nOpcao == 1
            Auth()

         CASE nOpcao == 2
            GeraPIX()

         CASE nOpcao == 3

         CASE nOpcao == 4
            RegistraBoleto()

         CASE nOpcao == 5
            ConsultaBoleto()
      ENDCASE
   ENDDO
RETURN


STATIC FUNCTION Auth()
   LOCAL hAuth := AutenticarAPI()

   IF ValType( hAuth ) == "H"
      QOut( "Token:" )
      QOut( hAuth[ "token" ] )
      QOut( )
      QOut( "Expiration:" )
      QOut( hAuth[ "expiration" ] )
      QOut( )
      QOut( "RefreshToken:" )
      IF HHasKey( hAuth, "refreshToken" )
         QOut( hAuth[ "refreshToken" ] )
      ELSE
         QOut( )
      ENDIF
      QOut( )
      QOut( "Type:" )
      IF HHasKey( hAuth, "type" )
         QOut( hAuth[ "type" ] )
      ELSE
         QOut( )
      ENDIF
      QOut( )
   ELSEIF ValType( hAuth ) == "O"
      QOut( FormatError( hAuth ) )
   ELSE
      QOut( "Erro: retorno inesperado" )
   ENDIF
   __Wait( )
RETURN


STATIC FUNCTION GeraPIX()
   LOCAL hPix := GerarPIX()

   IF ValType( hPix ) == "H"
      IF HHasKey( hPix, "QRCodeImage" )
         QOut( "Base64 QRCodeImage:" )
         QOut( hPix[ "QRCodeImage" ] )
         QOut( )
         QOut( "QRCodePIX retornado em Base64 pela API foi salvo como arquivo/imagem na subpasta QRCodePIX." )
         QOut( )
         QOut( )
      ENDIF

      IF HHasKey( hPix, "pixCopiaECola" )
         QOut( "pixCopiaECola: " + hPix[ "pixCopiaECola" ] )
         QOut( )
      ENDIF

      IF ! HHasKey( hPix, "QRCodeImage" ) .AND. ! HHasKey( hPix, "pixCopiaECola" )
         QOut( "Retorno OK, mas sem campos esperados." )
      ENDIF
   ELSEIF ValType( hPix ) == "O"
      QOut( FormatError( hPix ) )
   ELSE
      QOut( "Erro: retorno inesperado" )
   ENDIF
   __Wait( )
RETURN

STATIC FUNCTION RegistraBoleto()
   LOCAL hBoleto := RegistrarBoleto()

   IF ValType( hBoleto ) == "H"
      QOut( "Boleto registrado com sucesso:" )
      QOut( hb_jsonEncode( hBoleto ) )
   ELSEIF ValType( hBoleto ) == "O"
      QOut( FormatError( hBoleto ) )
   ELSE
      QOut( "Erro: retorno inesperado" )
   ENDIF
   __Wait( )
RETURN

STATIC FUNCTION ConsultaBoleto()
   LOCAL xBoleto := ConsultarBoleto()

   IF ValType( xBoleto ) == "H" .OR. ValType( xBoleto ) == "A"
      QOut( "Consulta de boleto concluida:" )
      QOut( hb_jsonEncode( xBoleto ) )
   ELSEIF ValType( xBoleto ) == "O"
      QOut( FormatError( xBoleto ) )
   ELSE
      QOut( "Erro: retorno inesperado" )
   ENDIF
   __Wait( )
RETURN





STATIC FUNCTION FormatError( oErr )
   LOCAL cMsg := "Erro"

   IF oErr == NIL
      RETURN cMsg
   ENDIF

   IF __objHasMsg( oErr, "Description" )
      cMsg += ": " + oErr:Description
   ENDIF

   IF __objHasMsg( oErr, "SubSystem" )
      cMsg += " [" + oErr:SubSystem + "]"
   ENDIF

   IF __objHasMsg( oErr, "GenCode" )
      cMsg += " Code=" + LTrim( Str( oErr:GenCode ) )
   ENDIF

   IF __objHasMsg( oErr, "SubCode" )
      cMsg += " SubCode=" + LTrim( Str( oErr:SubCode ) )
   ENDIF

   IF __objHasMsg( oErr, "Operation" )
      cMsg += " Op=" + oErr:Operation
   ENDIF

RETURN cMsg
